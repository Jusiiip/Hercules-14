-	script	Last Man Standing	FAKENPC,{

OnEnter:
	if ( .start == false ) {
		mes "Registration closed. Please come again.";
		close;
	}
	if ( queuesize(.qid) >= .register_limit ) {
		mes "this event has reach the maximum player participations.";
		close;
	}
	warp .eventmap$, 0,0;
	queueadd .qid, getcharid(3);
	percentheal 100, 100;
	end;
	
OnStart:
OnStartLMS:
	donpcevent "eventmanager::OnStartEvent";
	.start = true;
	.qid = queue();
	queueopt .qid, QUEUEOPT_DEATH, strnpcinfo(0)+"::OnDeath";
	queueopt .qid, QUEUEOPT_LOGOUT, strnpcinfo(0)+"::OnQuit";
	queueopt .qid, QUEUEOPT_MAPCHANGE, strnpcinfo(0)+"::OnMapChange";
	announce "[ Event Manager ]: The Last Man Standing Event will begin in 1 minute. Use @joinevent to join.", bc_all|bc_blue;
	sleep .register_timer * 60000;
	donpcevent "eventmanager::OnCloseJoinEvent";
	announce "[ Event Manager ]: Last Man Standing Event has begun! Registration is now closed.", bc_all|bc_blue;
	if ( queuesize(.qid) < .register_min ) {
		donpcevent "eventmanager::OnEndEvent";
		mapannounce .eventmap$, "[ Event Manager ]: Not enough player for Last Man Standing Event.", bc_all|bc_blue;
		queuedel .qid;
		mapwarp .eventmap$, "veil", 119,104;
		.start = false;
		end;
	}
	.start = false;
	sleep 3000; // waiting timer here
	mapannounce .eventmap$, "THIS   IS   SPARTA !!!!!", bc_all, C_RED, FW_BOLD, 50;
	pvpon .eventmap$;
	end;
OnDeath:
	if ( isloggedin( killerrid ) )
		announce "[ Event Manager ]: "+ strcharinfo(0) +" was killed by "+ rid2name( killerrid ), bc_map;
	else
		announce "[ Event Manager ]: "+ strcharinfo(0) +" was killed by a monster", bc_map;
	queueremove .qid, getcharid(3);
	warp "veil", 119,104;
	if ( queuesize(.qid) == 1 ) goto L_End;
	end;
OnMapChange:
	if ( @Queue_Destination_Map$ == .eventmap$ ) end;
OnQuit:
	announce "[ Event Manager ]: "+ strcharinfo(0) +" has Quit!", bc_map;
	queueremove .qid, getcharid(3);
	if ( queuesize(.qid) == 1 ) goto L_End;
	end;
L_End:
	.@it = queueiterator(.qid); // call the 1st member of the queue ID
	.@aid = qiget(.@it); // get the 1st index of the queue
	qiclear .@it; // remove the called memory
	attachrid .@aid;	
	donpcevent "eventmanager::OnEndEvent";
	announce "[ Event Manager ]: Congratulations ~ The winner of Last Man Standing event is "+ strcharinfo(0), bc_all|bc_blue;
	getitem 12281, 2;
	killmonsterall .eventmap$;
	pvpoff .eventmap$;
	queuedel .qid; // event ends, delete the queue from server
	sleep2 5000;
	warp "veil", 119,104;
	end;
	
OnInit:
	if ( C_RED != 0xFF0000 ) end; // the even simpler way to stop this script from loading, it will print 'Fatal Error ! No Player attached !'

	.eventmap$ = "guild_vs5";
	.register_min = 2; // minimum amount of players to start this event, or else it auto-abort
	.register_limit = 100; // maximum amount of players able to participate in this event
	.register_timer = 1; // how many minutes for player to register this event ?

	bindatcmd "lms", strnpcinfo(0)+"::OnStart", 99,100;
	getmapxy .map$, .x, .y, UNITTYPE_NPC;
	setarray .@mapflag,
		mf_nosave,
		mf_nowarp,
		mf_nowarpto,
		mf_nomemo,
		mf_nopenalty,
	//	mf_nobranch, // as far as I recall, some server allow players to use dead branch to make LMS more exciting
		mf_noicewall,
		mf_pvp_noparty,
		mf_pvp_noguild;
	.@mapflagsize = getarraysize( .@mapflag );
	for ( .@i = 0; .@i < .@mapflagsize; ++.@i )
		setmapflag .eventmap$, .@mapflag[.@i];
	end;
}