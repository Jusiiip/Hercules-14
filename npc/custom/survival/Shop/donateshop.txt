//===== Hercules Script ======================================
//= Euphy's Voucher Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6a
//===== Description: =========================================
//= A dynamic Voucher Shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "Vouchershop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
veil,81,136,6	script	Voucher Shop#1	10021,{ callfunc "Vouchershop"; }


// Script Core
//============================================================
-	script	Voucher_shop	FAKE_NPC,{
function Add; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,0;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,1;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Headgears","Armor","Weapon","Shield","Garment","Shoes","Accessories","Giftbox","Usable";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

	Add(1,5377,1,0,0,7179,200);		//Antique Smoking Pipe
	Add(1,5379,1,0,0,7179,50);		//Baloon Hat
	Add(1,5800,1,0,0,7179,100);		//Blush of Groom
	Add(1,5211,1,0,0,7179,50);		//Dresshat
	Add(1,5374,1,0,0,7179,100);		//Gigantic Majestic Horn
	Add(1,5423,1,0,0,7179,50);		//I love china
	Add(1,5372,1,0,0,7179,50);		//Koneko Hat
	Add(1,5518,1,0,0,7179,150);		//Large Majestic Goat
	Add(1,5013,1,0,0,7179,200);		//Lord Kaho's Horn
	Add(1,5457,1,0,0,7179,100);		//Moon Rabit's Hat
	Add(1,5305,1,0,0,7179,100);		//Pirate Dagger
	Add(1,5463,1,0,0,7179,100);		//Rainbow Scarf
	Add(1,5801,1,0,0,7179,100);		//Red Tailed Ribbon
	Add(1,5325,1,0,0,7179,100);		//Robo Eyes
	Add(1,18539,1,0,0,7179,150);		//Skull Cap
	Add(1,5805,1,0,0,7179,100);		//Victorious Coronet


	Add(2,2375,1,0,0,7179,50);		// Diabolus Armor
	Add(2,2374,1,0,0,7179,50);		// Diabolus Robe
	Add(2,2357,1,0,0,7179,50);		// Valk Amor

	Add(3,13033,1,0,0,7179,30);		// Assassin Dagger
	Add(3,1228,1,0,0,7179,100);		// Combat Knife
	Add(3,13414,1,0,0,7179,30);		// Elemental Sword
	Add(3,13017,1,0,0,7179,100);		// Icepick
	Add(3,13032,1,0,0,7179,50);		// Mailbreaker
	Add(3,2000,1,0,0,7179,30);		// Staff of Destruction
	Add(3,13031,1,0,0,7179,50);		// Swordbreaker
	Add(3,1636,1,0,0,7179,30);		// Thorn Staff of Darkness
	Add(3,13412,1,0,0,7179,100);		// Twin Edge of Naght (blue)
	Add(3,13413,1,0,0,7179,100);		// Twin Edge of Naght (red)


	Add(4,2138,1,0,0,7179,100);		// Bradium Shield
	Add(4,2130,1,0,0,7179,50);		// Cross Shield
	Add(4,2124,1,0,0,7179,100);		// Thorny Buckler
	Add(4,2115,1,0,0,7179,50);		// Valk Shield

	Add(5,2537,1,0,0,7179,50);		// D. Man2
	Add(5,2553,1,0,0,7179,50);		// Dragon Man2
	Add(5,2554,1,0,0,7179,150);		// Nidho
	Add(5,2524,1,0,0,7179,30);		// Valk Man2

	Add(6,2433,1,0,0,7179,50);		// Diabolus Shoes
	Add(6,2421,1,0,0,7179,10);		// Valkyrie Shoes
	Add(6,2423,1,0,0,7179,100);		// Variant Shoes


	Add(7,2729,1,0,0,7179,50);
	Add(7,2789,1,0,0,7179,50);
	Add(7,2790,1,0,0,7179,50);
	Add(7,2788,1,0,0,7179,50);
	Add(7,2702,1,0,0,7179,100);
	Add(7,2751,1,0,0,7179,50);
	Add(7,5825,1,0,0,7179,30);
	Add(7,2551,1,0,0,7179,30);

	//Add(8,33008,1,0,0,7179,20);
	//Add(8,33009,1,0,0,7179,20); 
	//Add(8,33010,1,0,0,7179,20);
	//Add(8,33011,1,0,0,7179,20);
	//Add(8,33012,1,0,0,7179,20);
	//Add(8,33013,1,0,0,7179,25);
	//Add(8,33014,1,0,0,7179,35);
	//Add(8,33015,1,0,0,7179,50); 

	Add(9,13697,1,0,0,7179,5);		//Blessing Box 10
	Add(9,13698,1,0,0,7179,5);		//Increase Agi Box 10
	Add(9,12214,1,0,0,7179,5);		//Convex
	Add(9,12210,1,0,0,7179,5);		//BBgum


// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "Vouchershop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(PC_NAME),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "Vouchershop"+.@i,1;
	npcshopattach "Vouchershop"+.@i;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(PC_NAME),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Voucher Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0], ITEMINFO_LOC), getiteminfo(.@q[0], ITEMINFO_VIEWSPRITE);
	if (@qe[2] > 0 && ((@qe[1] & EQP_HEAD_LOW) || (@qe[1] & EQP_HEAD_TOP) || (@qe[1] & EQP_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_TOP) || (@qe[1] & EQP_COSTUME_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_LOW) || (@qe[1] & EQP_GARMENT) || (@qe[1] & EQP_COSTUME_GARMENT)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(NPC_NAME)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+ getitemname(.@q[0]) +"^000000", ((.@preview && !@qe[7])?" ~ Preview...": ""), " ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Voucher Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Voucher Shop]";
				mes "^FF0000You need "+(((.@q[2] * getiteminfo(.@q[0], ITEMINFO_WEIGHT)) + Weight - MaxWeight) / 10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) Zeny -= (.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.Announce) announce strcharinfo(PC_NAME)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			specialeffect(EF_FLOWERLEAF, AREA, playerattached());
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		consolemes(CONSOLEMES_WARNING, "Quest reward #"+getarg(1)+" invalid (skipped).");
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			consolemes(CONSOLEMES_WARNING,  "Quest requirement #"+getarg(.@i)+" invalid (skipped).");
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "Vouchershop"+getarg(0),getarg(1),((.ShowZeny)?getarg(6):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0), ITEMINFO_TYPE) == IT_WEAPON || getiteminfo(getarg(0), ITEMINFO_TYPE) == IT_ARMOR) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	Vouchershop	{
	mes "[ Voucher Shop ]";
	mes "You can use your Survival Voucher in exchange for items.";
	mes "Please take a look what inside.";
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "Voucher_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	Vouchershop1	FAKE_NPC,909:-1
-	shop	Vouchershop2	FAKE_NPC,909:-1
-	shop	Vouchershop3	FAKE_NPC,909:-1
-	shop	Vouchershop4	FAKE_NPC,909:-1
-	shop	Vouchershop5	FAKE_NPC,909:-1
-	shop	Vouchershop6	FAKE_NPC,909:-1
-	shop	Vouchershop7	FAKE_NPC,909:-1
-	shop	Vouchershop8	FAKE_NPC,909:-1
-	shop	Vouchershop9	FAKE_NPC,909:-1
-	shop	Vouchershop10	FAKE_NPC,909:-1

